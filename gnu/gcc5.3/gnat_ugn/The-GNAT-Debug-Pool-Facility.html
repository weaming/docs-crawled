<html lang="en">
<head>
<title>The GNAT Debug Pool Facility - GNAT User's Guide for Native Platforms</title>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<meta name="description" content="GNAT User's Guide for Native Platforms">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="Memory-Management-Issues.html#Memory-Management-Issues" title="Memory Management Issues">
<link rel="prev" href="Some-Useful-Memory-Pools.html#Some-Useful-Memory-Pools" title="Some Useful Memory Pools">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--

     GNAT User's Guide for Native Platforms , March 24, 2015

     AdaCore

     Copyright (C) 2008-2015, Free Software Foundation
-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="The-GNAT-Debug-Pool-Facility"></a>
<p>
Previous:&nbsp;<a rel="previous" accesskey="p" href="Some-Useful-Memory-Pools.html#Some-Useful-Memory-Pools">Some Useful Memory Pools</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="Memory-Management-Issues.html#Memory-Management-Issues">Memory Management Issues</a>
<hr>
</div>

<p><a name="gnat_005fugn_002fgnat_005fand_005fprogram_005fexecution-id67"></a><a name="g_t256"></a><a name="gnat_005fugn_002fgnat_005fand_005fprogram_005fexecution-the_002dgnat_002ddebug_002dpool_002dfacility"></a><a name="g_t257"></a>

<h4 class="subsection">8.7.2 The GNAT Debug Pool Facility</h4>

<p><a name="index-Debug-Pool-1044"></a>
<a name="index-storage-1045"></a><a name="index-pool-1046"></a><a name="index-memory-corruption-1047"></a>
The use of unchecked deallocation and unchecked conversion can easily
lead to incorrect memory references. The problems generated by such
references are usually difficult to tackle because the symptoms can be
very remote from the origin of the problem. In such cases, it is
very helpful to detect the problem as early as possible. This is the
purpose of the Storage Pool provided by <cite>GNAT.Debug_Pools</cite>.

<p>In order to use the GNAT specific debugging pool, the user must
associate a debug pool object with each of the access types that may be
related to suspected memory problems. See Ada Reference Manual 13.11.

<blockquote>

<pre class="example">    type Ptr is access Some_Type;
    Pool : GNAT.Debug_Pools.Debug_Pool;
    for Ptr'Storage_Pool use Pool;
</pre>
</blockquote>

<p><cite>GNAT.Debug_Pools</cite> is derived from a GNAT-specific kind of
pool: the <cite>Checked_Pool</cite>. Such pools, like standard Ada storage pools,
allow the user to redefine allocation and deallocation strategies. They
also provide a checkpoint for each dereference, through the use of
the primitive operation <cite>Dereference</cite> which is implicitly called at
each dereference of an access value.

<p>Once an access type has been associated with a debug pool, operations on
values of the type may raise four distinct exceptions,
which correspond to four potential kinds of memory corruption:

     <ul>
<li><cite>GNAT.Debug_Pools.Accessing_Not_Allocated_Storage</cite>

     <li><cite>GNAT.Debug_Pools.Accessing_Deallocated_Storage</cite>

     <li><cite>GNAT.Debug_Pools.Freeing_Not_Allocated_Storage</cite>

     <li><cite>GNAT.Debug_Pools.Freeing_Deallocated_Storage</cite>
</ul>

<p>For types associated with a Debug_Pool, dynamic allocation is performed using
the standard GNAT allocation routine. References to all allocated chunks of
memory are kept in an internal dictionary. Several deallocation strategies are
provided, whereupon the user can choose to release the memory to the system,
keep it allocated for further invalid access checks, or fill it with an easily
recognizable pattern for debug sessions. The memory pattern is the old IBM
hexadecimal convention: <cite>16#DEADBEEF#</cite>.

<p>See the documentation in the file g-debpoo.ads for more information on the
various strategies.

<p>Upon each dereference, a check is made that the access value denotes a
properly allocated memory location. Here is a complete example of use of
<cite>Debug_Pools</cite>, that includes typical instances of  memory corruption:

<blockquote>

<pre class="example">    with Gnat.Io; use Gnat.Io;
    with Unchecked_Deallocation;
    with Unchecked_Conversion;
    with GNAT.Debug_Pools;
    with System.Storage_Elements;
    with Ada.Exceptions; use Ada.Exceptions;
    procedure Debug_Pool_Test is
    
       type T is access Integer;
       type U is access all T;
    
       P : GNAT.Debug_Pools.Debug_Pool;
       for T'Storage_Pool use P;
    
       procedure Free is new Unchecked_Deallocation (Integer, T);
       function UC is new Unchecked_Conversion (U, T);
       A, B : aliased T;
    
       procedure Info is new GNAT.Debug_Pools.Print_Info(Put_Line);
    
    begin
       Info (P);
       A := new Integer;
       B := new Integer;
       B := A;
       Info (P);
       Free (A);
       begin
          Put_Line (Integer'Image(B.all));
       exception
          when E : others =&gt; Put_Line ("raised: " &amp; Exception_Name (E));
       end;
       begin
          Free (B);
       exception
          when E : others =&gt; Put_Line ("raised: " &amp; Exception_Name (E));
       end;
       B := UC(A'Access);
       begin
          Put_Line (Integer'Image(B.all));
       exception
          when E : others =&gt; Put_Line ("raised: " &amp; Exception_Name (E));
       end;
       begin
          Free (B);
       exception
          when E : others =&gt; Put_Line ("raised: " &amp; Exception_Name (E));
       end;
       Info (P);
    end Debug_Pool_Test;
</pre>
</blockquote>

<p>The debug pool mechanism provides the following precise diagnostics on the
execution of this erroneous program:

<blockquote>

<pre class="example">    Debug Pool info:
      Total allocated bytes :  0
      Total deallocated bytes :  0
      Current Water Mark:  0
      High Water Mark:  0
    
    Debug Pool info:
      Total allocated bytes :  8
      Total deallocated bytes :  0
      Current Water Mark:  8
      High Water Mark:  8
    
    raised: GNAT.DEBUG_POOLS.ACCESSING_DEALLOCATED_STORAGE
    raised: GNAT.DEBUG_POOLS.FREEING_DEALLOCATED_STORAGE
    raised: GNAT.DEBUG_POOLS.ACCESSING_NOT_ALLOCATED_STORAGE
    raised: GNAT.DEBUG_POOLS.FREEING_NOT_ALLOCATED_STORAGE
    Debug Pool info:
      Total allocated bytes :  8
      Total deallocated bytes :  4
      Current Water Mark:  4
      High Water Mark:  8
</pre>
</blockquote>

<!--  Non-breaking space in running text -->
<!--  E.g. Ada |nbsp| 95 -->
</body></html>

