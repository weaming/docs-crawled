<html lang="en">
<head>
<title>Exporting Ada Entities - GNAT User's Guide for Native Platforms</title>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<meta name="description" content="GNAT User's Guide for Native Platforms">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="Building-DLLs-with-gnatdll.html#Building-DLLs-with-gnatdll" title="Building DLLs with gnatdll">
<link rel="prev" href="Limitations-When-Using-Ada-DLLs-from-Ada.html#Limitations-When-Using-Ada-DLLs-from-Ada" title="Limitations When Using Ada DLLs from Ada">
<link rel="next" href="Ada-DLLs-and-Elaboration.html#Ada-DLLs-and-Elaboration" title="Ada DLLs and Elaboration">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--

     GNAT User's Guide for Native Platforms , March 24, 2015

     AdaCore

     Copyright (C) 2008-2015, Free Software Foundation
-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="Exporting-Ada-Entities"></a>
<p>
Next:&nbsp;<a rel="next" accesskey="n" href="Ada-DLLs-and-Elaboration.html#Ada-DLLs-and-Elaboration">Ada DLLs and Elaboration</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="Limitations-When-Using-Ada-DLLs-from-Ada.html#Limitations-When-Using-Ada-DLLs-from-Ada">Limitations When Using Ada DLLs from Ada</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="Building-DLLs-with-gnatdll.html#Building-DLLs-with-gnatdll">Building DLLs with gnatdll</a>
<hr>
</div>

<p><a name="gnat_005fugn_002fplatform_005fspecific_005finformation-exporting_002dada_002dentities"></a><a name="g_t28d"></a><a name="gnat_005fugn_002fplatform_005fspecific_005finformation-id27"></a><a name="g_t291"></a>

<h5 class="subsubsection">9.3.5.14 Exporting Ada Entities</h5>

<p><a name="index-Export-table-1097"></a>
Building a DLL is a way to encapsulate a set of services usable from any
application. As a result, the Ada entities exported by a DLL should be
exported with the <cite>C</cite> or <cite>Stdcall</cite> calling conventions to avoid
any Ada name mangling. As an example here is an Ada package
<cite>API</cite>, spec and body, exporting two procedures, a function, and a
variable:

<blockquote>

<pre class="example">    with Interfaces.C; use Interfaces;
    package API is
       Count : C.int := 0;
       function Factorial (Val : C.int) return C.int;
    
       procedure Initialize_API;
       procedure Finalize_API;
       --  Initialization &amp; Finalization routines. More in the next section.
    private
       pragma Export (C, Initialize_API);
       pragma Export (C, Finalize_API);
       pragma Export (C, Count);
       pragma Export (C, Factorial);
    end API;
</pre>
<pre class="example">    package body API is
       function Factorial (Val : C.int) return C.int is
          Fact : C.int := 1;
       begin
          Count := Count + 1;
          for K in 1 .. Val loop
             Fact := Fact * K;
          end loop;
          return Fact;
       end Factorial;
    
       procedure Initialize_API is
          procedure Adainit;
          pragma Import (C, Adainit);
       begin
          Adainit;
       end Initialize_API;
    
       procedure Finalize_API is
          procedure Adafinal;
          pragma Import (C, Adafinal);
       begin
          Adafinal;
       end Finalize_API;
    end API;
</pre>
</blockquote>

<p>If the Ada DLL you are building will only be used by Ada applications
you do not have to export Ada entities with a <cite>C</cite> or <cite>Stdcall</cite>
convention. As an example, the previous package could be written as
follows:

<blockquote>

<pre class="example">    package API is
       Count : Integer := 0;
       function Factorial (Val : Integer) return Integer;
    
       procedure Initialize_API;
       procedure Finalize_API;
       --  Initialization and Finalization routines.
    end API;
</pre>
<pre class="example">    package body API is
       function Factorial (Val : Integer) return Integer is
          Fact : Integer := 1;
       begin
          Count := Count + 1;
          for K in 1 .. Val loop
             Fact := Fact * K;
          end loop;
          return Fact;
       end Factorial;
    
       ...
       --  The remainder of this package body is unchanged.
    end API;
</pre>
</blockquote>

<p>Note that if you do not export the Ada entities with a <cite>C</cite> or
<cite>Stdcall</cite> convention you will have to provide the mangled Ada names
in the definition file of the Ada DLL
(<a href="292.html#g_t292">Creating the Definition File</a>).

</body></html>

